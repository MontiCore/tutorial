/* (c) https://github.com/MontiCore/monticore */
package de.monticore.gettingstarted;

grammar SimpleOD extends de.monticore.types.MCSimpleGenericTypes,
                                        de.monticore.expressions.CommonExpressions,
                                        de.monticore.expressions.AssignmentExpressions,
                                        de.monticore.expressions.BitExpressions,
                                        de.monticore.literals.MCCommonLiterals,
                                        de.monticore.UMLModifier,
                                        de.monticore.symbols.OOSymbols {

  ODCompilationUnit = MCPackageDeclaration?
                        MCImportStatement*
                        ODArtifact;

  scope symbol ODArtifact implements Type = "objectdiagram" Name "{" ODElement* "}";

  interface ODElement;

  scope symbol ODNamedObject implements ODElement, ODValue, Variable =
                        Modifier?
                        Name ":" MCObjectType
                           "{" ODAttribute* "}";

  //TODO: symbol
  symbol ODAttribute implements Variable = Stereotype? Modifier? MCType? Name "=" ODValue ";";

  interface ODValue;

  ODAbsent implements ODValue = "...";

  ODSimpleValue implements ODValue = Expression;



}
